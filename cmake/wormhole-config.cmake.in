@PACKAGE_INIT@

macro(import_targets type)
    if(NOT EXISTS "${CMAKE_CURRENT_LIST_DIR}/wormhole-${type}-targets.cmake")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE "wormhole ${type} libraries were requested but not found")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND OFF)
        return()
    endif()

    include("${CMAKE_CURRENT_LIST_DIR}/wormhole-${type}-targets.cmake")
endmacro()

if(NOT TARGET wormhole::wormhole)
    set(type "")

    if(DEFINED WORMHOLE_USE_SHARED_LIBS)
        if(WORMHOLE_USE_SHARED_LIBS)
            set(type "shared")
        else()
            set(type "static")
        endif()
    elseif(BUILD_SHARED_LIBS AND EXISTS "${CMAKE_CURRENT_LIST_DIR}/wormhole-shared-targets.cmake")
        set(type "shared")
    elseif(EXISTS "${CMAKE_CURRENT_LIST_DIR}/wormhole-static-targets.cmake")
        set(type "static")
    else()
        set(type "shared")
    endif()

    import_targets(${type})
    check_required_components(wormhole)
    message("-- Found ${type} wormhole (version ${${CMAKE_FIND_PACKAGE_NAME}_VERSION})")
endif()
