@PACKAGE_INIT@

macro(import_targets type)
    if(NOT EXISTS "${CMAKE_CURRENT_LIST_DIR}/wormhole-${type}-targets.cmake")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE "wormhole ${type} libraries were requested but not found")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND OFF)
        return()
    endif()

    include("${CMAKE_CURRENT_LIST_DIR}/wormhole-${type}-targets.cmake")
endmacro()

if(NOT TARGET wormhole::wormhole AND NOT TARGET wormhole::libwormhole)
    set(_type "")

    if(DEFINED WORMHOLE_USE_SHARED_LIB)
        if(WORMHOLE_USE_SHARED_LIB)
            set(_type "shared")
        else()
            set(_type "static")
        endif()
    elseif(BUILD_SHARED_LIBS AND EXISTS "${CMAKE_CURRENT_LIST_DIR}/wormhole-shared-targets.cmake")
        set(_type "shared")
    elseif(EXISTS "${CMAKE_CURRENT_LIST_DIR}/wormhole-static-targets.cmake")
        set(_type "static")
    else()
        set(_type "shared")
    endif()

    import_targets(${_type})
    check_required_components(wormhole)

    string(TOUPPER "${CMAKE_BUILD_TYPE}" _build)

    get_target_property(wormhole_INCLUDE_DIRS wormhole::libwormhole INTERFACE_INCLUDE_DIRECTORIES)
    get_target_property(wormhole_LIBRARY wormhole::libwormhole IMPORTED_LOCATION_${_build})
    get_target_property(wormhole_BINRARY wormhole::wormhole IMPORTED_LOCATION_${_build})

    set(wormhole_FOUND ON)

    unset(_build)
    unset(_type)
endif()
